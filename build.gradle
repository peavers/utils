plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id "io.freefair.lombok" version "6.6.3"
    id 'fr.brouillard.oss.gradle.jgitver' version '0.9.1'
    id "org.sonarqube" version "3.5.0.2730"
}

group 'io.github.peavers'
description = 'Collection of things'

repositories {
    mavenCentral()
}

dependencies {
    // Resilience
    implementation "io.github.resilience4j:resilience4j-all:2.0.2"

    // Apache
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'commons-io:commons-io:2.12.0'

    // Logging
    implementation 'org.slf4j:slf4j-api:2.0.6'

    // Test
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    testImplementation 'org.mockito:mockito-core:5.2.0'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
}

sonar {
    properties {
        property "sonar.projectKey", "peavers-utils"
    }
}

normalization {
    runtimeClasspath {
        metaInf {
            ignoreAttribute("Build-Time")
            ignoreAttribute("Build-Date")
        }
    }
}

publishing {
    publications {
        javaLibrary(MavenPublication) {
            from(project.components.java)
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/peavers/utils")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
